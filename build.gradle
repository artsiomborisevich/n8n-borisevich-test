plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.owasp.dependencycheck' version '12.1.9'
    id 'com.github.ben-manes.versions' version '0.53.0'
}

group = 'net.idt'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.16.0'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.mapstruct:mapstruct:1.6.3'

    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.assertj:assertj-core:3.27.6'
}

tasks.named('test') {
    useJUnitPlatform()
}

// OWASP Dependency Check configuration
dependencyCheck {
    failBuildOnCVSS = 7
    suppressionFile = 'dependency-check-suppressions.xml'
    formats = ['HTML', 'JSON', 'SARIF']
    outputDirectory = 'build/reports/dependency-check'
}

// Dependency updates configuration
tasks.named('dependencyUpdates') {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

def isNonStable(String version) {
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}
